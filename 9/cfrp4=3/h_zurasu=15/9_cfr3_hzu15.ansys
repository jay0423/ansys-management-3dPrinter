finish
/clear,start
/NOPR

!-タイトル--------------------------------------------------
/TITLE,TEST

!-総合プリプロセッサ----------------------------------------
/PREP7


!-基本定義--------------------------------------------------
ANTYPE,STATIC        ! 静解析
ET,1,SOLID186       ! 試験片ALとCFRPにSOLID185を使用

/NOPR                !構造解析
KEYW,PR_SET,1   
KEYW,PR_STRUC,1 
KEYW,PR_THERM,0 
KEYW,PR_FLUID,0 
KEYW,PR_ELMAG,0 
KEYW,MAGNOD,0   
KEYW,MAGEDG,0   
KEYW,MAGHFE,0   
KEYW,MAGELC,0   
KEYW,PR_MULTI,0 
KEYW,PR_CFD,0   
/GO 

!-パラメータ定義--------------------------------------------
X1 = 80E-3 !試験片長さ
Z1 = 5E-3 !PLA厚さ
E_y = 12E-3 !エポキシ部の長さ
P_y = 14E-3 !PLA横の長さ
E_z = 3E-3 !エポキシ部の厚さ
H_midle_PLA = 1E-3 ! 真ん中のPLA部分の幅
H_zurasu = 15E-3 ! ずらず距離

CF_lap = 25E-3 ! CFRPの重ね継ぎ手長さ
CF_len = (X1 + CF_lap) / 2 !CFRP長さ
CF_radius = 0.564E-3 !CFRPの半径
CF_gap = 1.0E-3 ! CFRP間の距離
Y4 = P_y/2 - CF_gap/2 - CF_radius !CFRP1の中心y座標
Y5 = P_y/2 + CF_gap/2 !CFRP2の初期位置
H_CFRP_PLA_distance_y = (E_y-H_midle_PLA)/4-CF_radius-CF_gap/2

R_cf_top_h = 0.2 ! cfrp先端の比率
R_cf_small = 0.5 ! cfrpが細い部分のラップ長さの比率

X_cf_small_fir = CF_len - (R_cf_small / (1 / CF_lap))

DIV1=2.0E-3  !分割数1
DIV2=0.8E-3 !分割数2
DIV3=0.3E-3 !分割数3

DISTANCE = 2E-3
TIME1 = 2


!-材料物性定義----------------------------------------------
!>>>>>材料1>>>>>>>
! PLA
Aee1 = 2.78E9!全方向弾性率

Anu12 = 0.3 !全方向ポアソン比

Ag12 = 0.1E9   !全方向せん断弾性率

AsuA = -20E6 !圧縮強度
AsuXYZ = 55E6 !引張強度
SendanXYZ_1 = 20E6 !せん断許容応力
!MITUDO2= 2790   !質量密度


MP,EX,1,Aee1        ! 材料2:AL
MP,EY,1,Aee1  
MP,EZ,1,Aee1  
MP,PRXY,1,Anu12
MP,PRYZ,1,Anu12
MP,PRXZ,1,Anu12
MP,GXY,1,Ag12
MP,GYZ,1,Ag12
MP,GXZ,1,Ag12




!>>>>>材料2>>>>>>>
! エポキシ樹脂
! Bee1 = 2E9!全方向弾性率
Bee1 = 1E9!全方向弾性率

Bnu12 = 0.39 !全方向ポアソン比

Bg12 = 0.05E9   !全方向せん断弾性率

BsuA = -100E6 !圧縮強度
BsuXYZ = 70E6 !引張強度
SendanXYZ_2= 20E6 !せん断許容応力

!MITUDO2= 2790   !質量密度


MP,EX,2,Bee1        ! 材料2:AL
MP,EY,2,Bee1  
MP,EZ,2,Bee1  
MP,PRXY,2,Bnu12
MP,PRYZ,2,Bnu12
MP,PRXZ,2,Bnu12
MP,GXY,2,Bg12
MP,GYZ,2,Bg12
MP,GXZ,2,Bg12

!MP,DENS,2,MITUDO2

!>>>>>材料3>>>>>>>
! CFRP
Cee1 = 62.5E9!1方向弾性率
Cee2 = 6E9  !2方向弾性率
Cee3 = 6E9  !3方向弾性率

Cnu12 = 0.30 !zx,zy方向ポアソン比
Cnu13 = 0.30 !xz,yz方向ポアソン比
Cnu23 = 0.35 !xy,yx方向ポアソン比

Cg23 = 0.9E9 !2-3方向せん断弾性率
Cg12 = 4E9 !2-3方向せん断弾性率

CsuA = -100E9 !圧縮強度
CsuX = 965E6 !引張強度(繊維平行方向)
CsuZ = 19E6 !引張強度(繊維垂直方向)
CsuY = CsuZ !引張強度(繊維垂直レイアップ方向)
SendanXYZ = 90.4E6 !せん断許容応力

! MITUDO1=  1260  !質量密度



MP,EX,3,Cee1        ! 材料1:CFRTP
MP,EY,3,Cee3  
MP,EZ,3,Cee2 

MP,PRXY,3,Cnu12
MP,PRYZ,3,Cnu23
MP,PRXZ,3,Cnu13

! MP,NUXY,3,Cnu12
! MP,NUYZ,3,Cnu21
! MP,NUXZ,3,Cnu12


MP,GXY,3,Cg23
MP,GYZ,3,Cg12
MP,GXZ,3,Cg23

!MP,DENS,1,MITUDO1


!-損傷モデル定義------------------------------------------

!>>>>>材料1>>>>>>>
! PLA
TB,DMGI,1,,4,1			! 損傷発生の基準則を指定
TBDATA,1,2,2,2,2			! 最大応力則の指定

TB,FCLI,1,1,20,1			! 損傷発生の閾値を指定
TBDATA,1,AsuXYZ,AsuA,AsuXYZ,AsuA,AsuXYZ,AsuA! 応力指定
TBDATA,7,SendanXYZ_1,SendanXYZ_1,SendanXYZ_1

TB,DMGE,1,1,4,1			! 損傷発生時の剛性減少率を指定
TBDATA,1,1,1,1,1	! 損傷発生時の剛性減少率を指定

! TB,BKIN,1,1,2,1
! TBTEMP,0
! TBDATA,,35.1e6,2e9,,,,

!>>>>>材料2>>>>>>>
! epoxy
TB,DMGI,2,,4,1			! 損傷発生の基準則を指定
TBDATA,1,2,2,2,2			! 最大応力則の指定

TB,FCLI,2,1,20,1			! 損傷発生の閾値を指定
TBDATA,1,BsuXYZ,BsuA,BsuXYZ,BsuA,BsuXYZ,BsuA! 応力指定
TBDATA,7,SendanXYZ_2,SendanXYZ_2,SendanXYZ_2

TB,DMGE,2,1,4,1			! 損傷発生時の剛性減少率を指定
TBDATA,1,1,1,1,1	! 損傷発生時の剛性減少率を指定

! TB,BKIN,2,1,2,1
! TBTEMP,0
! TBDATA,,11.2e6,1.8e9,,,,

!>>>>>材料3>>>>>>>
! CFRP
TB,DMGI,3,,4,1			! 損傷発生の基準則を指定
TBDATA,1,2,2,2,2			! 最大応力則の指定

TB,FCLI,3,1,20,1			! 損傷発生の閾値を指定
TBDATA,1,CsuX,CsuA,CsuY,CsuA,CsuZ,CsuA! 応力指定
TBDATA,7,SendanXYZ,SendanXYZ,SendanXYZ

TB,DMGE,3,1,4,1			! 損傷発生時の剛性減少率を指定
TBDATA,1,1,1,1,1	! 損傷発生時の剛性減少率を指定



!-キーポイント定義------------------------------------------
! PLA
K	,	101	,	0	,	0	,	0
K	,	102	,	0	,	P_y	,	0
K	,	103	,	X1	,	P_y	,	0
K	,	104	,	X1	,	0	,	0
K	,	105	,	0	,	0	,	Z1
K	,	106	,	0	,	P_y	,	Z1
K	,	107	,	X1	,	P_y	,	Z1
K	,	108	,	X1	,	0	,	Z1
! PLAの真ん中の部分
K	,	109	,	0	,	(P_y-H_midle_PLA)/2	,	(Z1-E_z)/2
K	,	110	,	0	,	(P_y+H_midle_PLA)/2	,	(Z1-E_z)/2
K	,	111	,	X1	,	(P_y+H_midle_PLA)/2	,	(Z1-E_z)/2
K	,	112	,	X1	,	(P_y-H_midle_PLA)/2	,	(Z1-E_z)/2
K	,	113	,	0	,	(P_y-H_midle_PLA)/2	,	(Z1-E_z)/2+E_z
K	,	114	,	0	,	(P_y+H_midle_PLA)/2	,	(Z1-E_z)/2+E_z
K	,	115	,	X1	,	(P_y+H_midle_PLA)/2	,	(Z1-E_z)/2+E_z
K	,	116	,	X1	,	(P_y-H_midle_PLA)/2	,	(Z1-E_z)/2+E_z
! Epoxy
K       ,      201       ,       0       ,    (P_y-E_y)/2  ,       (Z1-E_z)/2
K       ,      202      ,       0       ,  (P_y-E_y)/2+E_y ,       (Z1-E_z)/2
K       ,      203      ,       X1      ,  (P_y-E_y)/2+E_y ,       (Z1-E_z)/2
K       ,      204      ,       X1      ,  (P_y-E_y)/2    ,       (Z1-E_z)/2
K       ,      205      ,       0       ,    (P_y-E_y)/2  ,       (Z1-E_z)/2+E_z
K       ,      206      ,       0       , (P_y-E_y)/2+E_y  ,       (Z1-E_z)/2+E_z
K       ,      207      ,       X1      ,  (P_y-E_y)/2+E_y ,       (Z1-E_z)/2+E_z
K       ,      208      ,       X1      ,  (P_y-E_y)/2    ,       (Z1-E_z)/2+E_z


!-ヴォリューム構築------------------------------------------------------

!---試験片--------------------------------------------
! PLA部分
V	,	101	,	102	,	103	,	104	,	105	,	106	,	107	,	108
ALLSEL
CM,SOTO,VOLU

! エポキシ樹脂部分
V	,	201	,	202	,	203	,	204	,	205	,	206	,	207	,	208
ALLSEL
CMSEL,U,SOTO
CM,UCHI,VOLU
ALLSEL
VOVLAP,ALL !重なり部分を削除
ALLSEL
CMSEL,U,UCHI
CM,SOTO,VOLU


V	,	109	,	110	,	111	,	112	,	113	,	114	,	115	,	116
ALLSEL
VOVLAP,ALL !重なり部分を削除
ALLSEL



! CFRP1部分
WPOFFS, 0, (P_y-E_y)/2+H_CFRP_PLA_distance_y, Z1/2
WPROTA, 0, 0, 90
CYLIND, CF_radius,0,0,X_cf_small_fir-H_zurasu
WPCSYS, -1
! CFRPの先端
WPOFFS, X_cf_small_fir-H_zurasu,(P_y-E_y)/2+H_CFRP_PLA_distance_y, Z1/2
WPROTA, 0, 0, 90
CONE,CF_radius,CF_radius*R_cf_top_h,CF_len-X_cf_small_fir
WPCSYS, -1
! CFRP2部分
WPOFFS, 0, (P_y-E_y)/2+H_CFRP_PLA_distance_y+CF_gap+CF_radius*2, Z1/2
WPROTA, 0, 0, 90
CYLIND, CF_radius,0,X1-X_cf_small_fir-H_zurasu,X1
WPCSYS, -1
! CFRPの先端
WPOFFS, X1-X_cf_small_fir-H_zurasu,(P_y-E_y)/2+H_CFRP_PLA_distance_y+CF_gap+CF_radius*2, Z1/2
WPROTA, 0, 0, -90
CONE,CF_radius,CF_radius*R_cf_top_h,CF_len-X_cf_small_fir
WPCSYS, -1


! CFRP3部分
WPOFFS, 0, (P_y+H_midle_PLA)/2+H_CFRP_PLA_distance_y, Z1/2
WPROTA, 0, 0, 90
CYLIND, CF_radius,0,0,X_cf_small_fir+H_zurasu
WPCSYS, -1
! CFRPの先端
WPOFFS, X_cf_small_fir+H_zurasu,(P_y+H_midle_PLA)/2+H_CFRP_PLA_distance_y, Z1/2
WPROTA, 0, 0, 90
CONE,CF_radius,CF_radius*R_cf_top_h,CF_len-X_cf_small_fir
WPCSYS, -1
! CFRP4部分
WPOFFS, 0, (P_y+H_midle_PLA)/2+H_CFRP_PLA_distance_y+CF_gap+CF_radius*2, Z1/2
WPROTA, 0, 0, 90
CYLIND, CF_radius,0,X1-X_cf_small_fir+H_zurasu,X1
WPCSYS, -1
! CFRPの先端
WPOFFS, X1-X_cf_small_fir+H_zurasu,(P_y+H_midle_PLA)/2+H_CFRP_PLA_distance_y+CF_gap+CF_radius*2, Z1/2
WPROTA, 0, 0, -90
CONE,CF_radius,CF_radius*R_cf_top_h,CF_len-X_cf_small_fir
WPCSYS, -1



ALLSEL
VOVLAP,ALL !重なり部分を削除
ALLSEL
VGLUE,ALL

! ボリュームの命名
VSEL,S,VOLU,,1,1
CM,PLA_midle,VOLU
VSEL,S,VOLU,,6,6
CM,SOTO,VOLU

VSEL,S,VOLU,,2,2
CM,CFRP1,VOLU
VSEL,S,VOLU,,3,3
CM,CFRP1_s,VOLU
VSEL,S,VOLU,,7,7
CM,CFRP2,VOLU
VSEL,S,VOLU,,4,4
CM,CFRP2_s,VOLU
VSEL,S,VOLU,,9,9
CM,CFRP3,VOLU
VSEL,S,VOLU,,5,5
CM,CFRP3_s,VOLU
VSEL,S,VOLU,,11,11
CM,CFRP4,VOLU
VSEL,S,VOLU,,8,8
CM,CFRP4_s,VOLU

VSEL,S,VOLU,,12,12
CM,Epo1,VOLU
VSEL,S,VOLU,,10,10
CM,Epo2,VOLU


!-メッシュ分割--------------------------------------------------

ALLSEL
AESIZE,ALL,DIV3


!-試験片CFRPメッシュ--------------------------------------------
CMSEL,S,CFRP1
TYPE,1                             !要素1
MAT,3                             !材料2
VSWEEP,ALL
MOPT,DEFA
! CM,CFRP1,ELEM
CMSEL,S,CFRP1_s
TYPE,1                             !要素1
MAT,3                             !材料2
VSWEEP,ALL
MOPT,DEFA
! CM,CFRP1,ELEM
CMSEL,S,CFRP2
TYPE,1                             !要素1
MAT,3                             !材料2
VSWEEP,ALL
MOPT,DEFA
! CM,CFRP2,ELEM
CMSEL,S,CFRP2_s
TYPE,1                             !要素1
MAT,3                             !材料2
VSWEEP,ALL
MOPT,DEFA
! CM,CFRP1,ELEM
CMSEL,S,CFRP3
TYPE,1                             !要素1
MAT,3                             !材料2
VSWEEP,ALL
MOPT,DEFA
! CM,CFRP2,ELEM
CMSEL,S,CFRP3_s
TYPE,1                             !要素1
MAT,3                             !材料2
VSWEEP,ALL
MOPT,DEFA
! CM,CFRP1,ELEM
CMSEL,S,CFRP4
TYPE,1                             !要素1
MAT,3                             !材料2
VSWEEP,ALL
MOPT,DEFA
! CM,CFRP2,ELEM
CMSEL,S,CFRP4_s
TYPE,1                             !要素1
MAT,3                             !材料2
VSWEEP,ALL
MOPT,DEFA
! CM,CFRP1,ELEM


!-試験片内側メッシュ--------------------------------------------
ALLSEL
AESIZE,ALL,DIV2

CMSEL,S,Epo1
TYPE,1                             !要素1
MAT,2                             !材料2
MSHAPE,1,3D
MSHKEY,0
VMESH,ALL
! VSWEEP,ALL
MOPT,DEFA
! CM,UCHI,ELEM

CMSEL,S,Epo2
TYPE,1                             !要素1
MAT,2                             !材料2
MSHAPE,1,3D
MSHKEY,0
VMESH,ALL
! VSWEEP,ALL
MOPT,DEFA
! CM,UCHI,ELEM


!-試験片外側メッシュ--------------------------------------------
ALLSEL
AESIZE,ALL,DIV1

CMSEL,S,PLA_midle
TYPE,1                             !要素1
MAT,1                             !材料1
MSHAPE,1,3D
MSHKEY,0
VMESH,ALL
! VSWEEP,ALL
MOPT,DEFA

CMSEL,S,SOTO
TYPE,1                             !要素1
MAT,1                             !材料1
MSHAPE,1,3D
MSHKEY,0
VMESH,ALL
! VSWEEP,ALL
MOPT,DEFA
! CM,SOTO,VOLU
ALLSEL


!-境界条件指定--------------------------------------------------
ALLSEL

ASEL,S,LOC,X,0 !グリップ固定
NSLA,S,1 
D,ALL,UZ,0
D,ALL,UX,0
D,ALL,UY,0

! ASEL,S,LOC,Y,0 !グリップ固定
! NSLA,S,1 
! D,ALL,UY,0

ASEL,S,LOC,X,X1 !荷重負荷面カップリング
NSLA,S,1
! D,ALL,UZ,0
! D,ALL,UY,0
CP,NEXT,UX,ALL

*GET,NNUM,NODE,,NUM,MIN !最小の節点選択
D,NNUM,UX,DISTANCE !節点引張距離




ALLSEL !節点全選択

!-解析------------------------------------------------------
FINISH  
/POST1

  
FINISH  
/SOLU



NSUBST,100,1000,100 ! データベースとファイルの書き込み制御の結果アイテム
TIME,TIME1

OUTRES,ERASE

!OUTRES,ALL,ALL  
OUTRES,NSOL,1    !節点の自由度の解
OUTRES,ESOL,1    !次の要素ソリューションアイテムをすべて有効または無効にする
OUTRES,RSOL,1    !節点反応荷重
OUTRES,NLOAD,1    !要素の節点、入力拘束、および力の荷重（POST1コマンドのPRRFOR、NFORCE、FSUMでも使用され、反力の荷重を計算します）。
OUTRES,MISC,1    !エレメントのその他のデータ（ETABLEコマンドのSMISCおよびNMISCアイテム）


SAVE