! CFRPを傘のような形状にして解析を行う
! Z方向には埋め込まれていない

finish
/clear,start
/NOPR

!-タイトル--------------------------------------------------
/TITLE,TEST

!-総合プリプロセッサ----------------------------------------
/PREP7


!-基本定義--------------------------------------------------
ANTYPE,STATIC        ! 静解析
ET,1,SOLID186       ! 試験片ALとCFRPにSOLID185を使用

/NOPR                !構造解析
KEYW,PR_SET,1   
KEYW,PR_STRUC,1 
KEYW,PR_THERM,0 
KEYW,PR_FLUID,0 
KEYW,PR_ELMAG,0 
KEYW,MAGNOD,0   
KEYW,MAGEDG,0   
KEYW,MAGHFE,0   
KEYW,MAGELC,0   
KEYW,PR_MULTI,0 
KEYW,PR_CFD,0   
/GO 

!-パラメータ定義--------------------------------------------
X1 = 80E-3 ! 試験片長さ
! Y1	=	25E-3
Z1 = 5E-3
P_y = 14E-3 ! PLAのy幅

E_y = 12E-3 ! Epoxyのy幅
E_z = 3E-3 ! Epoxyのz幅

CF_lap = 20E-3 ! CFRPの重ね継ぎ手長さ
CF_len = (X1 + CF_lap) / 2 ! CFRP1本あたりの長さ
CF_y = 1.41E-3 ! CFRPの高さ
CF_z = CF_y ! CFRPの幅
CF_gap = 0E-3 ! CFRP間の距離
Y4 = P_y/2 - CF_gap/2 - CF_z !CFRP1の初期位置
Y5 = P_y/2 + CF_gap/2 !CFRP2の初期位置

R_cf_top_h = 0.1 ! cfrp先端の比率
R_cf_small = 0.5 ! cfrpが細い部分のラップ長さの比率

X_cf_small_fir = CF_len - (R_cf_small / (1 / CF_lap))

ch_dis = 10E-3 ! cfrpが細くなってから継ぎ手部分を差し引いた距離
L_naname = 20E-3 ! cfrpの斜め繊維の長さ
alpha = 0.5E-3 ! plaと斜め繊維先端のy方向距離
R_cf_hoso = 0.8
X_hoso_fir = CF_len - CF_lap - ch_dis ! 細いcfrpのx座標
H_cf_hoso = CF_y / (1/R_cf_hoso) ! 細いcfrpの幅
H_cf_naname = (CF_y-H_cf_hoso)/2 ! 斜め方向cfrpの幅

! cfrp斜め繊維1つ目
sita1 = ASIN((((P_y+E_y)/2-(Y4+CF_y))-alpha)/L_naname)
X_naname_top1 = L_naname / (1/COS(sita1)) + X_hoso_fir
Y_naname_top1 = L_naname / (1/SIN(sita1)) + Y4+H_cf_naname+H_cf_hoso
! cfrp斜め繊維２つ目
sita2 = ASIN((Y4-(P_y-E_y)/2-alpha)/L_naname)
X_naname_top2 = L_naname / (1/COS(sita2)) + X_hoso_fir
Y_naname_top2 = Y4 - L_naname / (1/SIN(sita2))



DIV1=2.0E-3  !分割数1 エポキシ樹脂・PLA
DIV2=0.8E-3 !分割数2 CFRP

DISTANCE = 2E-3
TIME1 = 2


!-材料物性定義----------------------------------------------
!>>>>>材料1>>>>>>>
! PLA
Aee1 = 2.78E9!全方向弾性率

Anu12 = 0.3 !全方向ポアソン比

Ag12 = 0.1E9   !全方向せん断弾性率

AsuA = -20E6 !圧縮強度
AsuXYZ = 55E6 !引張強度
SendanXYZ_1 = 20E6 !せん断許容応力
!MITUDO2= 2790   !質量密度


MP,EX,1,Aee1        ! 材料2:AL
MP,EY,1,Aee1  
MP,EZ,1,Aee1  
MP,PRXY,1,Anu12
MP,PRYZ,1,Anu12
MP,PRXZ,1,Anu12
MP,GXY,1,Ag12
MP,GYZ,1,Ag12
MP,GXZ,1,Ag12




!>>>>>材料2>>>>>>>
! エポキシ樹脂
! Bee1 = 2E9!全方向弾性率
Bee1 = 0.4E9!全方向弾性率

Bnu12 = 0.39 !全方向ポアソン比

Bg12 = 0.05E9   !全方向せん断弾性率

BsuA = -100E6 !圧縮強度
BsuXYZ = 40E6 !引張強度
SendanXYZ_2= 20E6 !せん断許容応力

!MITUDO2= 2790   !質量密度


MP,EX,2,Bee1        ! 材料2:AL
MP,EY,2,Bee1  
MP,EZ,2,Bee1  
MP,PRXY,2,Bnu12
MP,PRYZ,2,Bnu12
MP,PRXZ,2,Bnu12
MP,GXY,2,Bg12
MP,GYZ,2,Bg12
MP,GXZ,2,Bg12

!MP,DENS,2,MITUDO2

!>>>>>材料3>>>>>>>
! CFRP
Cee1 = 62.5E9!1方向弾性率
Cee2 = 6E9  !2方向弾性率
Cee3 = 6E9  !3方向弾性率

Cnu12 = 0.30 !zx,zy方向ポアソン比
Cnu13 = 0.30 !xz,yz方向ポアソン比
Cnu23 = 0.35 !xy,yx方向ポアソン比

Cg23 = 0.9E9 !2-3方向せん断弾性率
Cg12 = 4E9 !2-3方向せん断弾性率

CsuA = -100E9 !圧縮強度
CsuX = 965E6 !引張強度(繊維平行方向)
CsuZ = 19E6 !引張強度(繊維垂直方向)
CsuY = CsuZ !引張強度(繊維垂直レイアップ方向)
SendanXYZ = 90.4E6 !せん断許容応力

! MITUDO1=  1260  !質量密度



MP,EX,3,Cee1        ! 材料1:CFRTP
MP,EY,3,Cee3  
MP,EZ,3,Cee2 

MP,PRXY,3,Cnu12
MP,PRYZ,3,Cnu23
MP,PRXZ,3,Cnu13

! MP,NUXY,3,Cnu12
! MP,NUYZ,3,Cnu21
! MP,NUXZ,3,Cnu12


MP,GXY,3,Cg23
MP,GYZ,3,Cg12
MP,GXZ,3,Cg23

!MP,DENS,1,MITUDO1


!-損傷モデル定義------------------------------------------

!>>>>>材料1>>>>>>>
! PLA
TB,DMGI,1,,4,1			! 損傷発生の基準則を指定
TBDATA,1,2,2,2,2			! 最大応力則の指定

TB,FCLI,1,1,20,1			! 損傷発生の閾値を指定
TBDATA,1,AsuXYZ,AsuA,AsuXYZ,AsuA,AsuXYZ,AsuA! 応力指定
TBDATA,7,SendanXYZ_1,SendanXYZ_1,SendanXYZ_1

TB,DMGE,1,1,4,1			! 損傷発生時の剛性減少率を指定
TBDATA,1,1,1,1,1	! 損傷発生時の剛性減少率を指定

! TB,BKIN,1,1,2,1
! TBTEMP,0
! TBDATA,,35.1e6,2e9,,,,


!>>>>>材料2>>>>>>>
! epoxy
TB,DMGI,2,,4,1			! 損傷発生の基準則を指定
TBDATA,1,2,2,2,2			! 最大応力則の指定

TB,FCLI,2,1,20,1			! 損傷発生の閾値を指定
TBDATA,1,BsuXYZ,BsuA,BsuXYZ,BsuA,BsuXYZ,BsuA! 応力指定
TBDATA,7,SendanXYZ_2,SendanXYZ_2,SendanXYZ_2

TB,DMGE,2,1,4,1			! 損傷発生時の剛性減少率を指定
TBDATA,1,1,1,1,1	! 損傷発生時の剛性減少率を指定

! TB,BKIN,2,1,2,1
! TBTEMP,0
! TBDATA,,11.2e6,1.8e9,,,,


!>>>>>材料3>>>>>>>
! CFRP
TB,DMGI,3,,4,1			! 損傷発生の基準則を指定
TBDATA,1,2,2,2,2			! 最大応力則の指定

TB,FCLI,3,1,20,1			! 損傷発生の閾値を指定
TBDATA,1,CsuX,CsuA,CsuY,CsuA,CsuZ,CsuA! 応力指定
TBDATA,7,SendanXYZ,SendanXYZ,SendanXYZ

TB,DMGE,3,1,4,1			! 損傷発生時の剛性減少率を指定
TBDATA,1,1,1,1,1	! 損傷発生時の剛性減少率を指定




!-キーポイント定義------------------------------------------
! PLA
K	,	1	,	0	,	0	,	0
K	,	2	,	0	,	P_y	,	0
K	,	3	,	X1	,	P_y	,	0
K	,	4	,	X1	,	0	,	0
K	,	5	,	0	,	0	,	Z1
K	,	6	,	0	,	P_y	,	Z1
K	,	7	,	X1	,	P_y	,	Z1
K	,	8	,	X1	,	0	,	Z1

! エポキシ樹脂
K       ,       9       ,       0       ,    (P_y-E_y)/2  ,       (Z1-E_z)/2
K       ,      10       ,       0       ,  (P_y-E_y)/2+E_y ,       (Z1-E_z)/2
K       ,      11       ,       X1      ,  (P_y-E_y)/2+E_y ,       (Z1-E_z)/2
K       ,      12       ,       X1      ,  (P_y-E_y)/2    ,       (Z1-E_z)/2
K       ,      13       ,       0       ,    (P_y-E_y)/2  ,       (Z1-E_z)/2+E_z
K       ,      14       ,       0       , (P_y-E_y)/2+E_y  ,       (Z1-E_z)/2+E_z
K       ,      15       ,       X1      ,  (P_y-E_y)/2+E_y ,       (Z1-E_z)/2+E_z
K       ,      16       ,       X1      ,  (P_y-E_y)/2    ,       (Z1-E_z)/2+E_z

! CFRP1本目
K17_x = 0
K17_y = Y4
K17_z = (Z1-CF_z)/2
K       ,      17       ,       K17_x       ,  K17_y  ,    K17_z
K18_x = 0
K18_y = Y4+CF_y
K       ,      18       ,       K18_x       ,  K18_y ,       K17_z
K19_x = X_hoso_fir
K19_y = Y4+CF_y
K       ,      19       ,       K19_x    ,  K19_y ,       K17_z
K20_x = X_hoso_fir
K20_y = Y4
K       ,      20       ,       K20_x    ,  K20_y    ,       K17_z
K21_z = (Z1-CF_z)/2+CF_z
K       ,      21       ,       K17_x       ,  K17_y  ,    K21_z
K       ,      22       ,       K18_x       ,  K18_y ,       K21_z
K       ,      23       ,       K19_x    ,  K19_y    ,       K21_z
K       ,      24       ,       K20_x    ,  K20_y    ,       K21_z
! CFRP細い部分1本目
K25_x = X_hoso_fir
K25_y = Y4+H_cf_naname
K       ,      25       ,       K25_x      ,    K25_y  ,       K17_z
K26_x = X_hoso_fir
K26_y = Y4+H_cf_naname+H_cf_hoso
K       ,      26       ,       K26_x    ,    K26_y  ,       K17_z
K27_x = CF_len
K27_y = Y4+H_cf_naname+H_cf_hoso
K       ,      27       ,       K27_x    ,    K27_y ,  K17_z
K28_x = CF_len
K28_y = Y4+H_cf_naname
K       ,      28       ,       K28_x    ,    K28_y  ,      K17_z
K       ,      29       ,       K25_x    ,    K25_y  ,      K21_z
K       ,      30       ,       K26_x    ,    K26_y  ,      K21_z
K       ,      31       ,       K27_x    ,    K27_y  ,      K21_z
K       ,      32       ,       K28_x    ,    K28_y  ,      K21_z
! CFRP1の傘部分1
K33_x = X_hoso_fir
K33_y = Y4+H_cf_naname+H_cf_hoso
K       ,      33       ,       K33_x    ,    K33_y  ,      K17_z
K34_x = X_hoso_fir
K34_y = Y4+CF_y
K       ,      34       ,       K34_x    ,    K34_y  ,      K17_z
K35_x = X_naname_top1
K35_y = Y_naname_top1+H_cf_naname/COS(sita1)
K       ,      35       ,       K35_x    ,    K35_y  ,      K17_z
K36_x = X_naname_top1
K36_y = Y_naname_top1
K       ,      36       ,       K36_x    ,    K36_y  ,      K17_z
K       ,      37       ,       K33_x    ,    K33_y  ,      K21_z
K       ,      38       ,       K34_x    ,    K34_y  ,      K21_z
K       ,      39       ,       K35_x    ,    K35_y  ,      K21_z
K       ,      40       ,       K36_x    ,    K36_y  ,      K21_z
! CFRP1の傘部分2
K41_x = X_hoso_fir
K41_y = Y4
K       ,      41       ,       K41_x      ,    K41_y  ,      K17_z
K42_x = X_hoso_fir
K42_y = Y4+H_cf_naname
K       ,      42       ,       K42_x    ,    K42_y  ,       K17_z
K43_x = X_naname_top2
K43_y = Y_naname_top2+H_cf_naname/COS(sita2)
K       ,      43       ,       K43_x    ,    K43_y ,      K17_z
K44_x = X_naname_top2
K44_y = Y_naname_top2
K       ,      44       ,       K44_x    ,    K44_y  ,      K17_z
K       ,      45       ,       K41_x    ,    K41_y  ,      K21_z
K       ,      46       ,       K42_x    ,    K42_y  ,      K21_z
K       ,      47       ,       K43_x    ,    K43_y  ,      K21_z
K       ,      48       ,       K44_x    ,    K44_y  ,      K21_z

! CFRP2
! 180°回転させたときの座標の計算 x:元の座標 Cx:回転の中心座標
! 傘部分の座標は座標の回転の式によって決める．
! X = -x + 2Cx
! Y = -y + 2Cx
Cx = X1/2
Cy = P_y/2

! CFRP2本目
K49_x = 0
K49_y = Y4
K49_z = (Z1-CF_z)/2
! K       ,      17       ,       K17_x       ,  K17_y  ,    K17_z
K50_x = 0
K50_y = Y4+CF_y
! K       ,      18       ,       K18_x       ,  K18_y ,       K17_z
K51_x = X_cf_small_fir
K51_y = Y4+CF_y
! K       ,      19       ,       K19_x    ,  K19_y ,       K17_z
K52_x = X_cf_small_fir
K52_y = Y4
! K       ,      20       ,       K20_x    ,  K20_y    ,       K17_z
K53_z = (Z1+CF_z)/2
! K       ,      21       ,       K17_x       ,  K17_y  ,    K21_z
! K       ,      22       ,       K18_x       ,  K18_y ,       K21_z
! K       ,      23       ,       K19_x    ,  K19_y    ,       K21_z
! K       ,      24       ,       K20_x    ,  K20_y    ,       K21_z
! CFRP先端部分
K57_x = X_cf_small_fir
K57_y = Y4
! K       ,      25       ,       K25_x      ,    K25_y  ,       K17_z
K58_x = X_cf_small_fir
K58_y = Y4+CF_y
! K       ,      26       ,       K26_x    ,    K26_y  ,       K17_z
K59_x = CF_len
K59_y = Y4+(CF_y+R_cf_top_h/(1/CF_y))/2
K59_z = (Z1+CF_z)/2-(CF_y+R_cf_top_h/(1/CF_y))/2
! K       ,      27       ,       K27_x    ,    K27_y ,  K27_z
K60_x = CF_len
K60_y = Y4+(CF_y-R_cf_top_h/(1/CF_y))/2
! K       ,      28       ,       K28_x    ,    K28_y  ,      K27_z
! K       ,      29       ,       K25_x    ,    K25_y  ,      K21_z
! K       ,      30       ,       K26_x    ,    K26_y  ,      K21_z
K61_z = (Z1-CF_z)/2+(CF_y+R_cf_top_h/(1/CF_y))/2
! K       ,      31       ,       K27_x    ,    K27_y  ,      K29_z
! K       ,      32       ,       K28_x    ,    K28_y  ,      K29_z
! CFRP2本目
K       ,      49       ,       -K49_x+2*Cx    ,    -K49_y+2*Cy  ,    K49_z
K       ,      50       ,       -K50_x+2*Cx    ,    -K50_y+2*Cy  ,    K49_z
K       ,      51       ,       -K51_x+2*Cx    ,    -K51_y+2*Cy  ,    K49_z
K       ,      52       ,       -K52_x+2*Cx    ,    -K52_y+2*Cy  ,    K49_z
K       ,      53       ,       -K49_x+2*Cx    ,    -K49_y+2*Cy  ,    K53_z
K       ,      54       ,       -K50_x+2*Cx    ,    -K50_y+2*Cy  ,    K53_z
K       ,      55       ,       -K51_x+2*Cx    ,    -K51_y+2*Cy  ,    K53_z
K       ,      56       ,       -K52_x+2*Cx    ,    -K52_y+2*Cy  ,    K53_z
! CFRP細い部分2本目
K       ,      57       ,       -K57_x+2*Cx    ,    -K57_y+2*Cy  ,    K49_z
K       ,      58       ,       -K58_x+2*Cx    ,    -K58_y+2*Cy  ,    K49_z
K       ,      59       ,       -K59_x+2*Cx    ,    -K59_y+2*Cy  ,    K59_z
K       ,      60       ,       -K60_x+2*Cx    ,    -K60_y+2*Cy  ,    K59_z
K       ,      61       ,       -K57_x+2*Cx    ,    -K57_y+2*Cy  ,    K53_z
K       ,      62       ,       -K58_x+2*Cx    ,    -K58_y+2*Cy  ,    K53_z
K       ,      63       ,       -K59_x+2*Cx    ,    -K59_y+2*Cy  ,    K61_z
K       ,      64       ,       -K60_x+2*Cx    ,    -K60_y+2*Cy  ,    K61_z



!-ヴォリューム構築------------------------------------------------------

!---試験片--------------------------------------------
! PLA部分
V	,	1	,	2	,	3	,	4	,	5	,	6	,	7	,	8
! エポキシ樹脂部分
V	,	9	,	10	,	11	,	12	,	13	,	14	,	15	,	16

ALLSEL
VOVLAP,ALL !重なり部分を削除

VSEL,S,VOLU,,3,3 ! PLA
CM,SOTO,VOLU

! CFRP1部分
V	,	17	,	18	,	19	,	20	,	21	,	22	,	23	,	24
V	,	25	,	26	,	27	,	28	,	29	,	30	,	31	,	32
V	,	33	,	34	,	35	,	36	,	37	,	38	,	39	,	40
V	,	41	,	42	,	43	,	44	,	45	,	46	,	47	,	48

! CFRP2本目
V	,	49	,	50	,	51	,	52	,	53	,	54	,	55	,	56
V	,	57	,	58	,	59	,	60	,	61	,	62	,	63	,	64

ALLSEL
VOVLAP,ALL !重なり部分を削除
VSEL,S,VOLU,,4,4
VSEL,A,VOLU,,9,10
CM,CFRP_K1,VOLU

VSEL,S,VOLU,,12,12
CM,CFRP1,VOLU

VSEL,S,VOLU,,7,7
VSEL,A,VOLU,,11,11
CM,CFRP2,VOLU


ALLSEL
CMSEL,U,SOTO
CMSEL,U,CFRP1
CMSEL,U,CFRP2
CMSEL,U,CFRP_K1
CM,UCHI,VOLU


ALLSEL
VGLUE,ALL


!-メッシュ分割--------------------------------------------------

ALLSEL
AESIZE,ALL,DIV2

ALLSEL

!-試験片CFRPメッシュ--------------------------------------------
! CFRP1斜め方向
CMSEL,S,CFRP_K1
TYPE,1                             !要素1
MAT,3                             !材料2
MSHAPE,0,3D
MSHKEY,1
VMESH,ALL
MOPT,DEFA
CM,CFRP_K1,VOLU

! CFRP1真ん中
CMSEL,S,CFRP1
TYPE,1                             !要素1
MAT,3                             !材料2
! VSWEEP,ALL
MSHAPE,1,3D
MSHKEY,O
VMESH,ALL
MOPT,DEFA
CM,CFRP1,VOLU

! CFRP2真ん中
CMSEL,S,CFRP2
TYPE,1                             !要素1
MAT,3                             !材料2
! VSWEEP,ALL
MSHAPE,1,3D
MSHKEY,O
VMESH,ALL
MOPT,DEFA
CM,CFRP2,VOLU


!-試験片内側メッシュ--------------------------------------------

CMSEL,S,UCHI
TYPE,1                             !要素1
MAT,2                             !材料2
MSHAPE,1,3D
MSHKEY,0
VMESH,ALL
! VSWEEP,ALL
MOPT,DEFA
CM,UCHI,VOLU

!-試験片外側メッシュ--------------------------------------------
ALLSEL
AESIZE,ALL,DIV1

CMSEL,S,SOTO
TYPE,1                             !要素1
MAT,1                             !材料1
MSHAPE,1,3D
MSHKEY,0
VMESH,ALL
! VSWEEP,ALL
MOPT,DEFA
CM,SOTO,VOLU
ALLSEL


!-境界条件指定--------------------------------------------------
ALLSEL

ASEL,S,LOC,X,0 !グリップ固定
NSLA,S,1 
D,ALL,UZ,0
D,ALL,UX,0
D,ALL,UY,0

! ASEL,S,LOC,Y,0 !グリップ固定
! NSLA,S,1 
! D,ALL,UY,0

ASEL,S,LOC,X,X1 !荷重負荷面カップリング
NSLA,S,1
CP,NEXT,UX,ALL

*GET,NNUM,NODE,,NUM,MIN !最小の節点選択
D,NNUM,UX,DISTANCE !節点引張距離




ALLSEL !節点全選択

!-解析------------------------------------------------------
FINISH  
/POST1

FINISH  
/SOLU



NSUBST,100,1000,100 ! データベースとファイルの書き込み制御の結果アイテム
TIME,TIME1

OUTRES,ERASE

!OUTRES,ALL,ALL  
OUTRES,NSOL,1    !節点の自由度の解
OUTRES,ESOL,1    !次の要素ソリューションアイテムをすべて有効または無効にする
OUTRES,RSOL,1    !節点反応荷重
OUTRES,NLOAD,1    !要素の節点、入力拘束、および力の荷重（POST1コマンドのPRRFOR、NFORCE、FSUMでも使用され、反力の荷重を計算します）。
OUTRES,MISC,1    !エレメントのその他のデータ（ETABLEコマンドのSMISCおよびNMISCアイテム）

SAVE