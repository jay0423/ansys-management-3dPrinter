finish
/clear,start
/NOPR

!-タイトル--------------------------------------------------
/TITLE,TEST

!-総合プリプロセッサ----------------------------------------
/PREP7


!-基本定義--------------------------------------------------
ANTYPE,STATIC        ! 静解析
ET,1,SOLID186       ! 試験片ALとCFRPにSOLID185を使用

/NOPR                !構造解析
KEYW,PR_SET,1   
KEYW,PR_STRUC,1 
KEYW,PR_THERM,0 
KEYW,PR_FLUID,0 
KEYW,PR_ELMAG,0 
KEYW,MAGNOD,0   
KEYW,MAGEDG,0   
KEYW,MAGHFE,0   
KEYW,MAGELC,0   
KEYW,PR_MULTI,0 
KEYW,PR_CFD,0   
/GO 

!-パラメータ定義--------------------------------------------
X1 = 80E-3 !試験片長さ
Z1 = 5E-3 !PLA厚さ
E_y = 12E-3 !エポキシ部の長さ
P_y = 14E-3 !PLA横の長さ
E_z = 3E-3 !エポキシ部の厚さ

CF_lap = 20E-3 ! CFRPの重ね継ぎ手長さ
CF_len = (X1 + CF_lap) / 2 !CFRP長さ
CF_y = 0.8E-3 !CFRPの厚さ
CF_z = CF_y !CFRPの横長さ(正方形)
CF_gap = 1.0E-3 ! CFRP間の距離
Y4 = P_y/2 - CF_gap/2 - CF_z !CFRP1の初期位置
Y5 = P_y/2 + CF_gap/2 !CFRP2の初期位置

DIV1=2.0E-3  !分割数1
DIV2=0.8E-3 !分割数2

DISTANCE = 2E-3
TIME1 = 2


!-材料物性定義----------------------------------------------
!>>>>>材料1>>>>>>>
! PLA
Aee1 = 2.78E9!全方向弾性率

Anu12 = 0.3 !全方向ポアソン比

Ag12 = 0.1E9   !全方向せん断弾性率

AsuA = -20E6 !圧縮強度
AsuXYZ = 55E6 !引張強度
SendanXYZ_1 = 20E6 !せん断許容応力
!MITUDO2= 2790   !質量密度


MP,EX,1,Aee1        ! 材料2:AL
MP,EY,1,Aee1  
MP,EZ,1,Aee1  
MP,PRXY,1,Anu12
MP,PRYZ,1,Anu12
MP,PRXZ,1,Anu12
MP,GXY,1,Ag12
MP,GYZ,1,Ag12
MP,GXZ,1,Ag12




!>>>>>材料2>>>>>>>
! エポキシ樹脂
! Bee1 = 2E9!全方向弾性率
Bee1 = 1E9!全方向弾性率

Bnu12 = 0.39 !全方向ポアソン比

Bg12 = 0.05E9   !全方向せん断弾性率

BsuA = -100E6 !圧縮強度
BsuXYZ = 70E6 !引張強度
SendanXYZ_2= 20E6 !せん断許容応力

!MITUDO2= 2790   !質量密度


MP,EX,2,Bee1        ! 材料2:AL
MP,EY,2,Bee1  
MP,EZ,2,Bee1  
MP,PRXY,2,Bnu12
MP,PRYZ,2,Bnu12
MP,PRXZ,2,Bnu12
MP,GXY,2,Bg12
MP,GYZ,2,Bg12
MP,GXZ,2,Bg12

!MP,DENS,2,MITUDO2

!>>>>>材料3>>>>>>>
! CFRP
Cee1 = 62.5E9!1方向弾性率
Cee2 = 6E9  !2方向弾性率
Cee3 = 6E9  !3方向弾性率

Cnu12 = 0.30 !zx,zy方向ポアソン比
Cnu13 = 0.30 !xz,yz方向ポアソン比
Cnu23 = 0.35 !xy,yx方向ポアソン比

Cg23 = 0.9E9 !2-3方向せん断弾性率
Cg12 = 4E9 !2-3方向せん断弾性率

CsuA = -100E9 !圧縮強度
CsuX = 965E6 !引張強度(繊維平行方向)
CsuZ = 19E6 !引張強度(繊維垂直方向)
CsuY = CsuZ !引張強度(繊維垂直レイアップ方向)
SendanXYZ = 90.4E6 !せん断許容応力

! MITUDO1=  1260  !質量密度



MP,EX,3,Cee1        ! 材料1:CFRTP
MP,EY,3,Cee3  
MP,EZ,3,Cee2 

MP,PRXY,3,Cnu12
MP,PRYZ,3,Cnu23
MP,PRXZ,3,Cnu13

! MP,NUXY,3,Cnu12
! MP,NUYZ,3,Cnu21
! MP,NUXZ,3,Cnu12


MP,GXY,3,Cg23
MP,GYZ,3,Cg12
MP,GXZ,3,Cg23

!MP,DENS,1,MITUDO1


!-損傷モデル定義------------------------------------------

!>>>>>材料1>>>>>>>
! PLA
TB,DMGI,1,,4,1			! 損傷発生の基準則を指定
TBDATA,1,2,2,2,2			! 最大応力則の指定

TB,FCLI,1,1,20,1			! 損傷発生の閾値を指定
TBDATA,1,AsuXYZ,AsuA,AsuXYZ,AsuA,AsuXYZ,AsuA! 応力指定
TBDATA,7,SendanXYZ_1,SendanXYZ_1,SendanXYZ_1

TB,DMGE,1,1,4,1			! 損傷発生時の剛性減少率を指定
TBDATA,1,1,1,1,1	! 損傷発生時の剛性減少率を指定

! TB,BKIN,1,1,2,1
! TBTEMP,0
! TBDATA,,35.1e6,2e9,,,,

!>>>>>材料2>>>>>>>
! epoxy
TB,DMGI,2,,4,1			! 損傷発生の基準則を指定
TBDATA,1,2,2,2,2			! 最大応力則の指定

TB,FCLI,2,1,20,1			! 損傷発生の閾値を指定
TBDATA,1,BsuXYZ,BsuA,BsuXYZ,BsuA,BsuXYZ,BsuA! 応力指定
TBDATA,7,SendanXYZ_2,SendanXYZ_2,SendanXYZ_2

TB,DMGE,2,1,4,1			! 損傷発生時の剛性減少率を指定
TBDATA,1,1,1,1,1	! 損傷発生時の剛性減少率を指定

! TB,BKIN,2,1,2,1
! TBTEMP,0
! TBDATA,,11.2e6,1.8e9,,,,

!>>>>>材料3>>>>>>>
! CFRP
TB,DMGI,3,,4,1			! 損傷発生の基準則を指定
TBDATA,1,2,2,2,2			! 最大応力則の指定

TB,FCLI,3,1,20,1			! 損傷発生の閾値を指定
TBDATA,1,CsuX,CsuA,CsuY,CsuA,CsuZ,CsuA! 応力指定
TBDATA,7,SendanXYZ,SendanXYZ,SendanXYZ

TB,DMGE,3,1,4,1			! 損傷発生時の剛性減少率を指定
TBDATA,1,1,1,1,1	! 損傷発生時の剛性減少率を指定



!-キーポイント定義------------------------------------------
! PLA
K	,	1	,	0	,	0	,	0
K	,	2	,	0	,	P_y	,	0
K	,	3	,	X1	,	P_y	,	0
K	,	4	,	X1	,	0	,	0
K	,	5	,	0	,	0	,	Z1
K	,	6	,	0	,	P_y	,	Z1
K	,	7	,	X1	,	P_y	,	Z1
K	,	8	,	X1	,	0	,	Z1
! エポキシ樹脂1
K       ,       9       ,       0       ,    Y5  ,       (Z1-E_z)/2
K       ,      10       ,       0       ,      E_y+(P_y-E_y)/2       ,       (Z1-E_z)/2
K       ,      11       ,       X1-CF_len   ,      E_y+(P_y-E_y)/2       ,       (Z1-E_z)/2
K       ,      12       ,       X1-CF_len   ,    Y5  ,       (Z1-E_z)/2
K       ,      13       ,       0       ,    Y5  ,       (Z1-E_z)/2+E_z
K       ,      14       ,       0       ,      E_y+(P_y-E_y)/2       ,       (Z1-E_z)/2+E_z
K       ,      15       ,       X1-CF_len   ,      E_y+(P_y-E_y)/2       ,       (Z1-E_z)/2+E_z
K       ,      16       ,       X1-CF_len   ,    Y5  ,       (Z1-E_z)/2+E_z
! エポキシ樹脂2
K       ,      17       ,       X1-CF_len   ,    Y5  ,       (Z1-E_z)/2
K       ,      18       ,       X1-CF_len   ,      E_y+(P_y-E_y)/2       ,       (Z1-E_z)/2
K       ,      19       ,       X1      ,      E_y+(P_y-E_y)/2       ,       (Z1-E_z)/2
K       ,      20       ,       X1      ,  Y5    ,       (Z1-E_z)/2
K       ,      21       ,       X1-CF_len   ,    Y5  ,       (Z1-E_z)/2+E_z
K       ,      22       ,       X1-CF_len   ,      E_y+(P_y-E_y)/2       ,       (Z1-E_z)/2+E_z
K       ,      23       ,       X1      ,      E_y+(P_y-E_y)/2       ,       (Z1-E_z)/2+E_z
K       ,      24       ,       X1      ,  Y5    ,       (Z1-E_z)/2+E_z
! エポキシ樹脂3
K       ,      25       ,       0       ,    (P_y-E_y)/2        ,       (Z1-E_z)/2
K       ,      26       ,       0       ,    Y4+CF_z  ,       (Z1-E_z)/2
K       ,      27       ,       CF_len      ,    Y4+CF_z  ,       (Z1-E_z)/2
K       ,      28       ,       CF_len      ,    (P_y-E_y)/2    ,       (Z1-E_z)/2
K       ,      29       ,       0       ,    (P_y-E_y)/2 ,       (Z1-E_z)/2+E_z
K       ,      30       ,       0       ,    Y4+CF_z  ,       (Z1-E_z)/2+E_z
K       ,      31       ,       CF_len      ,    Y4+CF_z  ,       (Z1-E_z)/2+E_z
K       ,      32       ,       CF_len      ,      (P_y-E_y)/2        ,       (Z1-E_z)/2+E_z
! エポキシ樹脂4
K       ,      33       ,       CF_len      ,      (P_y-E_y)/2        ,       (Z1-E_z)/2
K       ,      34       ,       CF_len      ,    Y4+CF_z  ,       (Z1-E_z)/2
K       ,      35       ,       X1      ,    Y4+CF_z  ,       (Z1-E_z)/2
K       ,      36       ,       X1      ,      (P_y-E_y)/2        ,       (Z1-E_z)/2
K       ,      37       ,       CF_len      ,      (P_y-E_y)/2        ,       (Z1-E_z)/2+E_z
K       ,      38       ,       CF_len      ,    Y4+CF_z  ,       (Z1-E_z)/2+E_z
K       ,      39       ,       X1      ,    Y4+CF_z  ,       (Z1-E_z)/2+E_z
K       ,      40       ,       X1      ,      (P_y-E_y)/2        ,       (Z1-E_z)/2+E_z
! エポキシ樹脂5 真ん中
K       ,      41       ,       0      ,    Y4+CF_z     ,       (Z1-E_z)/2
K       ,      42       ,       0      ,    Y5  ,       (Z1-E_z)/2
K       ,      43       ,       X1      ,    Y5  ,       (Z1-E_z)/2
K       ,      44       ,       X1      ,     Y4+CF_z        ,       (Z1-E_z)/2
K       ,      45       ,       0      ,     Y4+CF_z        ,       (Z1-E_z)/2+E_z
K       ,      46       ,       0      ,    Y5   ,       (Z1-E_z)/2+E_z
K       ,      47       ,       X1      ,    Y5   ,       (Z1-E_z)/2+E_z
K       ,      48       ,       X1      ,     Y4+CF_z        ,       (Z1-E_z)/2+E_z
! CFRP1本目
K       ,      49       ,       0       ,      Y4       ,       (Z1-CF_y)/2
K       ,      50       ,       0       ,   Y4+CF_z   ,       (Z1-CF_y)/2
K       ,      51       ,       CF_len      ,   Y4+CF_z   ,       (Z1-CF_y)/2
K       ,      52       ,       CF_len      ,      Y4       ,       (Z1-CF_y)/2
K       ,      53       ,       0       ,      Y4       ,       (Z1-CF_y)/2+CF_y
K       ,      54       ,       0       ,    Y4+CF_z  ,       (Z1-CF_y)/2+CF_y
K       ,      55       ,       CF_len      ,    Y4+CF_z  ,       (Z1-CF_y)/2+CF_y
K       ,      56       ,       CF_len      ,      Y4       ,       (Z1-CF_y)/2+CF_y
! CFRP2本目
K       ,      57       ,     X1-CF_len     ,      Y5       ,       (Z1-CF_y)/2
K       ,      58       ,     X1-CF_len     ,  Y5+CF_z    ,       (Z1-CF_y)/2
K       ,      59       ,       X1      ,  Y5+CF_z    ,       (Z1-CF_y)/2
K       ,      60       ,       X1      ,      Y5       ,       (Z1-CF_y)/2
K       ,      61       ,     X1-CF_len     ,      Y5       ,       (Z1-CF_y)/2+CF_y
K       ,      62       ,     X1-CF_len     ,   Y5+CF_z   ,       (Z1-CF_y)/2+CF_y
K       ,      63       ,       X1      ,   Y5+CF_z   ,       (Z1-CF_y)/2+CF_y
K       ,      64       ,       X1      ,      Y5       ,       (Z1-CF_y)/2+CF_y


!-ヴォリューム構築------------------------------------------------------

!---試験片--------------------------------------------
! PLA部分
V	,	1	,	2	,	3	,	4	,	5	,	6	,	7	,	8
! エポキシ樹脂部分
V	,	9	,	10	,	11	,	12	,	13	,	14	,	15	,	16
V	,	17	,	18	,	19	,	20	,	21	,	22	,	23	,	24
V	,	25	,	26	,	27	,	28	,	29	,	30	,	31	,	32
V	,	33	,	34	,	35	,	36	,	37	,	38	,	39	,	40
V	,	41	,	42	,	43	,	44	,	45	,	46	,	47	,	48

ALLSEL
VOVLAP,ALL !重なり部分を削除

VSEL,S,VOLU,,10,10 ! PLA
CM,SOTO,VOLU
ALLSEL

! CFRP部分
V	,	49	,	50	,	51	,	52	,	53	,	54	,	55	,	56
V	,	57	,	58	,	59	,	60	,	61	,	62	,	63	,	64
VSEL,S,VOLU,,1,1
VSEL,A,VOLU,,3,3
CM,CFRP,VOLU
ALLSEL
VOVLAP,ALL !重なり部分を削除

VSEL,S,VOLU,,10,10
CM,SOTO,VOLU

! ボリューム名で選択し，名前を追加する．
VSEL,S,VOLU,,5,5
CM,UCHI1,VOLU
VSEL,S,VOLU,,12,12
CM,UCHI2,VOLU
VSEL,S,VOLU,,11,11
CM,UCHI3,VOLU
VSEL,S,VOLU,,6,6
CM,UCHI4,VOLU
VSEL,S,VOLU,,13,13
CM,UCHI5,VOLU


ALLSEL
VGLUE,ALL

! !-CF部のローカル座標定義--------------------------------------------------

! LOCAL,11,0,0,0,0,0,0,90
! CSYS,0

! !-CF部の繊維方向定義--------------------------------------------------
! !>>>>1層目>>>>>>>>>>>>
! CMSEL,S,CFRP
! VATT,3,,1,11

!-メッシュ分割--------------------------------------------------

! CMSEL,S,CFRP
ALLSEL
AESIZE,ALL,DIV2

ALLSEL

!-試験片CFRPメッシュ--------------------------------------------
CMSEL,S,CFRP
TYPE,1                             !要素1
MAT,3                             !材料2
! MSHAPE,0,3D
! MSHKEY,1
! VMESH,ALL
VSWEEP,ALL
MOPT,DEFA
CM,CFRP,VOLU

!-試験片内側メッシュ--------------------------------------------

CMSEL,S,UCHI5
TYPE,1                             !要素1
MAT,2                             !材料2
VSWEEP,ALL
MOPT,DEFA
CM,UCHI5,VOLU
ALLSEL

CMSEL,S,UCHI2
TYPE,1                             !要素1
MAT,2                             !材料2
VSWEEP,ALL
MOPT,DEFA
CM,UCHI2,VOLU
ALLSEL

CMSEL,S,UCHI3
TYPE,1                             !要素1
MAT,2                             !材料2
VSWEEP,ALL
MOPT,DEFA
CM,UCHI3,VOLU
ALLSEL

CMSEL,S,UCHI1
TYPE,1                             !要素1
MAT,2                             !材料2
VSWEEP,ALL
MOPT,DEFA
CM,UCHI1,VOLU
ALLSEL

CMSEL,S,UCHI4
TYPE,1                             !要素1
MAT,2                             !材料2
VSWEEP,ALL
MOPT,DEFA
CM,UCHI4,VOLU
ALLSEL

!-試験片外側メッシュ--------------------------------------------
ALLSEL
AESIZE,ALL,DIV1

CMSEL,S,SOTO
TYPE,1                             !要素1
MAT,1                             !材料1
! MSHAPE,0,3D
! MSHKEY,1
! VMESH,ALL
VSWEEP,ALL
MOPT,DEFA
CM,SOTO,VOLU
ALLSEL


!-境界条件指定--------------------------------------------------
ALLSEL

ASEL,S,LOC,X,0 !グリップ固定
NSLA,S,1 
D,ALL,UZ,0
D,ALL,UX,0
D,ALL,UY,0

! ASEL,S,LOC,Y,0 !グリップ固定
! NSLA,S,1 
! D,ALL,UY,0

ASEL,S,LOC,X,X1 !荷重負荷面カップリング
NSLA,S,1
! D,ALL,UZ,0
! D,ALL,UY,0
CP,NEXT,UX,ALL

*GET,NNUM,NODE,,NUM,MIN !最小の節点選択
D,NNUM,UX,DISTANCE !節点引張距離




ALLSEL !節点全選択

!-解析------------------------------------------------------
FINISH  
/POST1

  
FINISH  
/SOLU



NSUBST,100,1000,100 ! データベースとファイルの書き込み制御の結果アイテム
TIME,TIME1

OUTRES,ERASE

!OUTRES,ALL,ALL  
OUTRES,NSOL,1    !節点の自由度の解
OUTRES,ESOL,1    !次の要素ソリューションアイテムをすべて有効または無効にする
OUTRES,RSOL,1    !節点反応荷重
OUTRES,NLOAD,1    !要素の節点、入力拘束、および力の荷重（POST1コマンドのPRRFOR、NFORCE、FSUMでも使用され、反力の荷重を計算します）。
OUTRES,MISC,1    !エレメントのその他のデータ（ETABLEコマンドのSMISCおよびNMISCアイテム）


SAVE