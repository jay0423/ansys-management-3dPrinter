! CFRPを傘のような形状にして解析を行う
! Z方向には埋め込まれていない

finish
/clear,start
/NOPR

!-タイトル--------------------------------------------------
/TITLE,TEST

!-総合プリプロセッサ----------------------------------------
/PREP7


!-基本定義--------------------------------------------------
ANTYPE,STATIC        ! 静解析
ET,1,SOLID186       ! 試験片ALとCFRPにSOLID185を使用

/NOPR                !構造解析
KEYW,PR_SET,1   
KEYW,PR_STRUC,1 
KEYW,PR_THERM,0 
KEYW,PR_FLUID,0 
KEYW,PR_ELMAG,0 
KEYW,MAGNOD,0   
KEYW,MAGEDG,0   
KEYW,MAGHFE,0   
KEYW,MAGELC,0   
KEYW,PR_MULTI,0 
KEYW,PR_CFD,0   
/GO 

!-パラメータ定義--------------------------------------------
X1 = 80E-3 ! 試験片長さ
! Y1	=	25E-3
Z1 = 5E-3
P_y = 14E-3 ! PLAのy幅

E_y = 12E-3 ! Epoxyのy幅
E_z = 3E-3 ! Epoxyのz幅

CF_lap = 20E-3 ! CFRPの重ね継ぎ手長さ
CF_len = (X1 + CF_lap) / 2 ! CFRP1本あたりの長さ
CF_y = 1.0E-3 ! CFRPの高さ
CF_z = CF_y ! CFRPの幅
CF_gap = 1.0E-3 ! CFRP間の距離
Y4 = P_y/2 - CF_gap/2 - CF_z !CFRP1の初期位置
Y5 = P_y/2 + CF_gap/2 !CFRP2の初期位置

K_sita_do = 15 !角度
K_sita = K_sita_do / (1/3.141592) / 180
K_x = ((P_y-CF_y)/2 - (P_y-E_y)/2) / TAN(K_sita) ! CFRP傘部分長さ
K_h = 0.4E-3 ! CFRPの傘の幅
K_a = K_h/SIN(K_sita) !CFRP傘の角度θの幅
K_b = K_h/COS(K_sita) !CFRP傘の角度θの幅2

K_s = X1 - CF_len - ((CF_gap + CF_y) / TAN(K_sita)) - (K_h / SIN(K_sita))
alpha = 0.1E-3

DIV1=1.0E-3  !分割数1 エポキシ樹脂・PLA
DIV2=0.8E-3 !分割数2 CFRP

DISTANCE = 2E-3
TIME1 = 2


!-材料物性定義----------------------------------------------
!>>>>>材料1>>>>>>>
! PLA
Aee1 = 1.95E9!全方向弾性率

Anu12 = 0.3 !全方向ポアソン比

Ag12 = 0.1E9   !全方向せん断弾性率

AsuA = -20E6 !圧縮強度
AsuXYZ = 37E6 !引張強度
SendanXYZ_1 = 20E6 !せん断許容応力
!MITUDO2= 2790   !質量密度


MP,EX,1,Aee1        ! 材料2:AL
MP,EY,1,Aee1  
MP,EZ,1,Aee1  
MP,PRXY,1,Anu12
MP,PRYZ,1,Anu12
MP,PRXZ,1,Anu12
MP,GXY,1,Ag12
MP,GYZ,1,Ag12
MP,GXZ,1,Ag12




!>>>>>材料2>>>>>>>
! エポキシ樹脂
! Bee1 = 2.0E9!全方向弾性率
Bee1 = 0.19E9!全方向弾性率

Bnu12 = 0.39 !全方向ポアソン比

Bg12 = 0.05E9   !全方向せん断弾性率

BsuA = -100E6 !圧縮強度
BsuXYZ = 18.8E6 !引張強度
SendanXYZ_2= 20E6 !せん断許容応力

!MITUDO2= 2790   !質量密度


MP,EX,2,Bee1        ! 材料2:AL
MP,EY,2,Bee1  
MP,EZ,2,Bee1  
MP,PRXY,2,Bnu12
MP,PRYZ,2,Bnu12
MP,PRXZ,2,Bnu12
MP,GXY,2,Bg12
MP,GYZ,2,Bg12
MP,GXZ,2,Bg12

!MP,DENS,2,MITUDO2

!>>>>>材料3>>>>>>>
! CFRP
Cee1 = 62.5E9!1方向弾性率
Cee2 = 6E9  !2方向弾性率
Cee3 = 6E9  !3方向弾性率

Cnu12 = 0.30 !zx,zy方向ポアソン比
Cnu13 = 0.30 !xz,yz方向ポアソン比
Cnu23 = 0.35 !xy,yx方向ポアソン比

Cg23 = 0.9E9 !2-3方向せん断弾性率
Cg12 = 4E9 !2-3方向せん断弾性率

CsuA = -100E9 !圧縮強度
CsuX = 965E6 !引張強度(繊維平行方向)
CsuZ = 19E6 !引張強度(繊維垂直方向)
CsuY = CsuZ !引張強度(繊維垂直レイアップ方向)
SendanXYZ = 90.4E6 !せん断許容応力

! MITUDO1=  1260  !質量密度



MP,EX,3,Cee1        ! 材料1:CFRTP
MP,EY,3,Cee3  
MP,EZ,3,Cee2 
 
MP,PRXY,3,Cnu12
MP,PRYZ,3,Cnu23
MP,PRXZ,3,Cnu13

! MP,NUXY,3,Cnu12
! MP,NUYZ,3,Cnu21
! MP,NUXZ,3,Cnu12


MP,GXY,3,Cg23
MP,GYZ,3,Cg12
MP,GXZ,3,Cg23

!MP,DENS,1,MITUDO1


!-損傷モデル定義------------------------------------------

!>>>>>材料1>>>>>>>
! PLA
TB,DMGI,1,,4,1			! 損傷発生の基準則を指定
TBDATA,1,2,2,2,2			! 最大応力則の指定

TB,FCLI,1,1,20,1			! 損傷発生の閾値を指定
TBDATA,1,AsuXYZ,AsuA,AsuXYZ,AsuA,AsuXYZ,AsuA! 応力指定
TBDATA,7,SendanXYZ_1,SendanXYZ_1,SendanXYZ_1

TB,DMGE,1,1,4,1			! 損傷発生時の剛性減少率を指定
TBDATA,1,1,1,1,1	! 損傷発生時の剛性減少率を指定

! TB,BKIN,1,1,2,1
! TBTEMP,0
! TBDATA,,35.1e6,2e9,,,,


!>>>>>材料2>>>>>>>
! epoxy
TB,DMGI,2,,4,1			! 損傷発生の基準則を指定
TBDATA,1,2,2,2,2			! 最大応力則の指定

TB,FCLI,2,1,20,1			! 損傷発生の閾値を指定
TBDATA,1,BsuXYZ,BsuA,BsuXYZ,BsuA,BsuXYZ,BsuA! 応力指定
TBDATA,7,SendanXYZ_2,SendanXYZ_2,SendanXYZ_2

TB,DMGE,2,1,4,1			! 損傷発生時の剛性減少率を指定
TBDATA,1,1,1,1,1	! 損傷発生時の剛性減少率を指定

! TB,BKIN,2,1,2,1
! TBTEMP,0
! TBDATA,,11.2e6,1.8e9,,,,


!>>>>>材料3>>>>>>>
! CFRP
TB,DMGI,3,,4,1			! 損傷発生の基準則を指定
TBDATA,1,2,2,2,2			! 最大応力則の指定

TB,FCLI,3,1,20,1			! 損傷発生の閾値を指定
TBDATA,1,CsuX,CsuA,CsuY,CsuA,CsuZ,CsuA! 応力指定
TBDATA,7,SendanXYZ,SendanXYZ,SendanXYZ

TB,DMGE,3,1,4,1			! 損傷発生時の剛性減少率を指定
TBDATA,1,1,1,1,1	! 損傷発生時の剛性減少率を指定




!-キーポイント定義------------------------------------------
! PLA
K	,	1	,	0	,	0	,	0
K	,	2	,	0	,	P_y	,	0
K	,	3	,	X1	,	P_y	,	0
K	,	4	,	X1	,	0	,	0
K	,	5	,	0	,	0	,	Z1
K	,	6	,	0	,	P_y	,	Z1
K	,	7	,	X1	,	P_y	,	Z1
K	,	8	,	X1	,	0	,	Z1

! エポキシ樹脂
K       ,       9       ,       0       ,    (P_y-E_y)/2  ,       (Z1-E_z)/2
K       ,      10       ,       0       ,  (P_y-E_y)/2+E_y ,       (Z1-E_z)/2
K       ,      11       ,       X1      ,  (P_y-E_y)/2+E_y ,       (Z1-E_z)/2
K       ,      12       ,       X1      ,  (P_y-E_y)/2    ,       (Z1-E_z)/2
K       ,      13       ,       0       ,    (P_y-E_y)/2  ,       (Z1-E_z)/2+E_z
K       ,      14       ,       0       , (P_y-E_y)/2+E_y  ,       (Z1-E_z)/2+E_z
K       ,      15       ,       X1      ,  (P_y-E_y)/2+E_y ,       (Z1-E_z)/2+E_z
K       ,      16       ,       X1      ,  (P_y-E_y)/2    ,       (Z1-E_z)/2+E_z

! CFRP1本目
K       ,      17       ,       0       ,  Y4  ,       (Z1-CF_z)/2
K       ,      18       ,       0       ,  Y4+CF_y ,       (Z1-CF_z)/2
K       ,      19       ,       CF_len    ,  Y4+CF_y ,       (Z1-CF_z)/2
K       ,      20       ,       CF_len    ,  Y4    ,       (Z1-CF_z)/2
K       ,      21       ,       0       ,  Y4  ,       (Z1-CF_z)/2+CF_z
K       ,      22       ,       0       ,  Y4+CF_y  ,       (Z1-CF_z)/2+CF_z
K       ,      23       ,       CF_len      ,  Y4+CF_y ,       (Z1-CF_z)/2+CF_z
K       ,      24       ,       CF_len      ,  Y4    ,       (Z1-CF_z)/2+CF_z
! CFRP1の傘部分1
K       ,      25       ,       K_s      ,    Y4  ,       (Z1-CF_z)/2 + (CF_z-K_h)/2
K       ,      26       ,       K_a + K_s    ,    Y4  ,       (Z1-CF_z)/2 + (CF_z-K_h)/2
K       ,      27       ,       K_x + K_s    ,    (P_y - E_y)/2 + K_b + alpha ,  (Z1-CF_z)/2 + (CF_z-K_h)/2
K       ,      28       ,       K_x + K_s    ,    (P_y - E_y)/2 + alpha   ,       (Z1-CF_z)/2 + (CF_z-K_h)/2
K       ,      29       ,       K_s      ,    Y4  ,       (Z1-CF_z)/2 + (CF_z-K_h)/2 + K_h
K       ,      30       ,       K_a + K_s    ,    Y4  ,       (Z1-CF_z)/2 + (CF_z-K_h)/2 + K_h
K       ,      31       ,       K_x + K_s    ,    (P_y - E_y)/2 + K_b + alpha ,  (Z1-CF_z)/2 + (CF_z-K_h)/2 + K_h
K       ,      32       ,       K_x + K_s    ,    (P_y - E_y)/2 + alpha   ,      (Z1-CF_z)/2 + (CF_z-K_h)/2 + K_h
! CFRP1の傘部分2
K       ,      33       ,       K_s      ,    Y4 + CF_y  ,       (Z1-CF_z)/2 + (CF_z-K_h)/2
K       ,      34       ,       K_x + K_s    ,    P_y - (P_y - E_y)/2 - alpha   ,       (Z1-CF_z)/2 + (CF_z-K_h)/2
K       ,      35       ,       K_x + K_s    ,    P_y - (P_y - E_y)/2 - K_b - alpha ,  (Z1-CF_z)/2 + (CF_z-K_h)/2
K       ,      36       ,       K_a + K_s    ,    Y4 + CF_y  ,       (Z1-CF_z)/2 + (CF_z-K_h)/2
K       ,      37       ,       K_s      ,    Y4 + CF_y  ,       (Z1-CF_z)/2 + (CF_z-K_h)/2 + K_h
K       ,      38       ,       K_x + K_s    ,    P_y - (P_y - E_y)/2 - alpha   ,      (Z1-CF_z)/2 + (CF_z-K_h)/2 + K_h
K       ,      39       ,       K_x + K_s    ,    P_y - (P_y - E_y)/2 - K_b - alpha ,  (Z1-CF_z)/2 + (CF_z-K_h)/2 + K_h
K       ,      40       ,       K_a + K_s    ,    Y4 + CF_y  ,       (Z1-CF_z)/2 + (CF_z-K_h)/2 + K_h

! CFRP2本目
K       ,      41       ,     X1-CF_len     ,      Y5       ,       (Z1-CF_z)/2
K       ,      42       ,     X1-CF_len     ,  Y5+CF_y    ,       (Z1-CF_z)/2
K       ,      43       ,       X1      ,  Y5+CF_y    ,       (Z1-CF_z)/2
K       ,      44       ,       X1      ,      Y5       ,       (Z1-CF_z)/2
K       ,      45       ,     X1-CF_len     ,      Y5       ,       (Z1-CF_z)/2+CF_z
K       ,      46       ,     X1-CF_len     ,   Y5+CF_y   ,       (Z1-CF_z)/2+CF_z
K       ,      47       ,       X1      ,   Y5+CF_y   ,       (Z1-CF_z)/2+CF_z
K       ,      48       ,       X1      ,      Y5       ,       (Z1-CF_z)/2+CF_z
! CFRP2の傘部分1
! 180°回転させたときの座標の計算 x:元の座標 Cx:回転の中心座標
! 傘部分の座標は座標の回転の式によって決める．
! X = -x + 2Cx
! Y = -y + 2Cx
Cx = X1/2
Cy = P_y/2
K       ,      49       ,       -K_s+2*Cx      ,    -Y4+2*Cy  ,       (Z1-CF_z)/2 + (CF_z-K_h)/2
K       ,      50       ,       -(K_a + K_s)+2*Cx    ,    -Y4+2*Cy  ,       (Z1-CF_z)/2 + (CF_z-K_h)/2
K       ,      51       ,       -(K_x + K_s)+2*Cx    ,    -((P_y - E_y)/2 + K_b + alpha)+2*Cy ,  (Z1-CF_z)/2 + (CF_z-K_h)/2
K       ,      52       ,       -(K_x + K_s)+2*Cx    ,    -((P_y - E_y)/2 + alpha)+2*Cy   ,       (Z1-CF_z)/2 + (CF_z-K_h)/2
K       ,      53       ,       -K_s+2*Cx      ,    -Y4+2*Cy  ,       (Z1-CF_z)/2 + (CF_z-K_h)/2 + K_h
K       ,      54       ,       -(K_a + K_s)+2*Cx    ,    -Y4+2*Cy  ,       (Z1-CF_z)/2 + (CF_z-K_h)/2 + K_h
K       ,      55       ,       -(K_x + K_s)+2*Cx    ,    -((P_y - E_y)/2 + K_b + alpha)+2*Cy ,  (Z1-CF_z)/2 + (CF_z-K_h)/2 + K_h
K       ,      56       ,       -(K_x + K_s)+2*Cx    ,    -((P_y - E_y)/2 + alpha)+2*Cy   ,      (Z1-CF_z)/2 + (CF_z-K_h)/2 + K_h
! CFRP2の傘部分2
K       ,      57       ,       -K_s+2*Cx     ,    -(Y4 + CF_y)+2*Cy  ,       (Z1-CF_z)/2 + (CF_z-K_h)/2
K       ,      58       ,       -(K_x + K_s)+2*Cx    ,    -(P_y - (P_y - E_y)/2 - alpha)+2*Cy   ,       (Z1-CF_z)/2 + (CF_z-K_h)/2
K       ,      59       ,       -(K_x + K_s)+2*Cx    ,    -(P_y - (P_y - E_y)/2 - K_b - alpha)+2*Cy ,  (Z1-CF_z)/2 + (CF_z-K_h)/2
K       ,      60       ,       -(K_a + K_s)+2*Cx    ,    -(Y4 + CF_y)+2*Cy  ,       (Z1-CF_z)/2 + (CF_z-K_h)/2
K       ,      61       ,       -K_s+2*Cx      ,    -(Y4 + CF_y)+2*Cy  ,       (Z1-CF_z)/2 + (CF_z-K_h)/2 + K_h
K       ,      62       ,       -(K_x + K_s)+2*Cx    ,    -(P_y - (P_y - E_y)/2 - alpha)+2*Cy   ,      (Z1-CF_z)/2 + (CF_z-K_h)/2 + K_h
K       ,      63       ,       -(K_x + K_s)+2*Cx    ,    -(P_y - (P_y - E_y)/2 - K_b - alpha)+2*Cy ,  (Z1-CF_z)/2 + (CF_z-K_h)/2 + K_h
K       ,      64       ,       -(K_a + K_s)+2*Cx    ,    -(Y4 + CF_y)+2*Cy  ,       (Z1-CF_z)/2 + (CF_z-K_h)/2 + K_h


!-ヴォリューム構築------------------------------------------------------

!---試験片--------------------------------------------
! PLA部分
V	,	1	,	2	,	3	,	4	,	5	,	6	,	7	,	8
! エポキシ樹脂部分
V	,	9	,	10	,	11	,	12	,	13	,	14	,	15	,	16

ALLSEL
VOVLAP,ALL !重なり部分を削除

VSEL,S,VOLU,,3,3 ! PLA
CM,SOTO,VOLU

! CFRP部分
V	,	17	,	18	,	19	,	20	,	21	,	22	,	23	,	24
V	,	25	,	26	,	27	,	28	,	29	,	30	,	31	,	32
V	,	33	,	34	,	35	,	36	,	37	,	38	,	39	,	40

! CFRP2本目
V	,	41	,	42	,	43	,	44	,	45	,	46	,	47	,	48
V	,	49	,	50	,	51	,	52	,	53	,	54	,	55	,	56
V	,	57	,	58	,	59	,	60	,	61	,	62	,	63	,	64

ALLSEL
VOVLAP,ALL !重なり部分を削除
VSEL,S,VOLU,,4,5
CM,CFRP_K1,VOLU

VSEL,S,VOLU,,9,9
CM,CFRP1,VOLU

VSEL,S,VOLU,,7,8
CM,CFRP_K2,VOLU

VSEL,S,VOLU,,10,10
CM,CFRP2,VOLU



ALLSEL
CMSEL,U,SOTO
CMSEL,U,CFRP1
CMSEL,U,CFRP2
CMSEL,U,CFRP_K1
CMSEL,U,CFRP_K2
CM,UCHI,VOLU


ALLSEL
VGLUE,ALL


!-メッシュ分割--------------------------------------------------

ALLSEL
AESIZE,ALL,DIV2

ALLSEL

!-試験片CFRPメッシュ--------------------------------------------
! CFRP1斜め方向
CMSEL,S,CFRP_K1
TYPE,1                             !要素1
MAT,3                             !材料2
MSHAPE,0,3D
MSHKEY,1
VMESH,ALL
MOPT,DEFA
CM,CFRP_K1,VOLU

! CFRP1真ん中
CMSEL,S,CFRP1
TYPE,1                             !要素1
MAT,3                             !材料2
! VSWEEP,ALL
MSHAPE,1,3D
MSHKEY,O
VMESH,ALL
MOPT,DEFA
CM,CFRP1,VOLU

! CFRP2斜め方向
CMSEL,S,CFRP_K2
TYPE,1                             !要素1
MAT,3                             !材料2
MSHAPE,0,3D
MSHKEY,1
VMESH,ALL
MOPT,DEFA
CM,CFRP_K2,VOLU

! CFRP2真ん中
CMSEL,S,CFRP2
TYPE,1                             !要素1
MAT,3                             !材料2
! VSWEEP,ALL
MSHAPE,1,3D
MSHKEY,O
VMESH,ALL
MOPT,DEFA
CM,CFRP2,VOLU


!-試験片内側メッシュ--------------------------------------------

CMSEL,S,UCHI
TYPE,1                             !要素1
MAT,2                             !材料2
MSHAPE,1,3D
MSHKEY,0
VMESH,ALL
! VSWEEP,ALL
MOPT,DEFA
CM,UCHI,VOLU

!-試験片外側メッシュ--------------------------------------------
ALLSEL
AESIZE,ALL,DIV1

CMSEL,S,SOTO
TYPE,1                             !要素1
MAT,1                             !材料1
MSHAPE,1,3D
MSHKEY,0
VMESH,ALL
! VSWEEP,ALL
MOPT,DEFA
CM,SOTO,VOLU
ALLSEL


!-境界条件指定--------------------------------------------------
ALLSEL

ASEL,S,LOC,X,0 !グリップ固定
NSLA,S,1 
D,ALL,UZ,0
D,ALL,UX,0
D,ALL,UY,0

! ASEL,S,LOC,Y,0 !グリップ固定
! NSLA,S,1 
! D,ALL,UY,0

ASEL,S,LOC,X,X1 !荷重負荷面カップリング
NSLA,S,1
CP,NEXT,UX,ALL

*GET,NNUM,NODE,,NUM,MIN !最小の節点選択
D,NNUM,UX,DISTANCE !節点引張距離




ALLSEL !節点全選択

!-解析------------------------------------------------------
FINISH  
/POST1

FINISH  
/SOLU



NSUBST,100,1000,100 ! データベースとファイルの書き込み制御の結果アイテム
TIME,TIME1

OUTRES,ERASE

!OUTRES,ALL,ALL  
OUTRES,NSOL,1    !節点の自由度の解
OUTRES,ESOL,1    !次の要素ソリューションアイテムをすべて有効または無効にする
OUTRES,RSOL,1    !節点反応荷重
OUTRES,NLOAD,1    !要素の節点、入力拘束、および力の荷重（POST1コマンドのPRRFOR、NFORCE、FSUMでも使用され、反力の荷重を計算します）。
OUTRES,MISC,1    !エレメントのその他のデータ（ETABLEコマンドのSMISCおよびNMISCアイテム）

SAVE